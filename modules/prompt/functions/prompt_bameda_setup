#
# A single line theme with Git information on the left and Python on the right.
# Based on skwp theme.
#
# Authors:
#   David Barragán <bameda@fbarragan.com>
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#
#

function prompt_bameda_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Check for activated virtualenv
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Check for current node version
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_bameda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_bameda_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_bameda_colors=(
      "%F{230}" # Cosmic Latte
      "%F{141}" # Purpple
      "%F{218}" # Hotpink
      "%F{154}" # Limegreen
      "%F{208}" # Orange
    )
  else
    _prompt_bameda_colors=(
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{orange}"
    )
  fi
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_bameda_git_colors=(
      "%F{81}"   # branch name - turquoise
      "%F{70}"   # add         - green
      "%F{220}"  # modified    - yellow
      "%F{160}"  # deleted     - red
      "%F{45}"   # untracked   - blue
    )
  else
    _prompt_bameda_git_colors=(
      "%F{cyan}"    # branch name
      "%F{green}"   # add
      "%F{yellow}"  # modified
      "%F{red}"     # deleted
      "%F{blue}"    # untracked
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format "${_prompt_bameda_git_colors[1]}%s %f"
  zstyle ':prezto:module:git:info:branch' format "${_prompt_bameda_git_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_bameda_git_colors[2]}✚%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_bameda_git_colors[3]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_bameda_git_colors[4]}✖%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_bameda_git_colors[5]}✭%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'


  # Set node-info
  zstyle ':prezto:module:node:info:version' format "[${_prompt_bameda_colors[5]}%v%f] "

  # Set python-info (virtualenv name).
  zstyle ':prezto:module:python:info:virtualenv' format "[${_prompt_bameda_colors[3]}%v%f] "

  # Define prompts.
  PROMPT='$node_info[version]${python_info[virtualenv]}'"${_prompt_bameda_colors[2]}%n%f@${_prompt_bameda_colors[1]}%m%f ${_prompt_bameda_colors[4]}%3~%f $ "
  RPROMPT='${git_info:+${(e)git_info[prompt]}}'
}

prompt_bameda_setup "$@"
